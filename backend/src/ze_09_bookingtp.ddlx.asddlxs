@Metadata.layer: #CUSTOMER
@Search.searchable: true

@UI.selectionVariant: [{
    text: 'Bookings',
    qualifier: 'bookings'
 }]

@UI.headerInfo:{
    title.value: 'FlightClass',
    description.value: 'FormName',
    typeName: 'Booking',
    typeNamePlural: 'Bookings'
}

annotate view ZC_09_BookingTP with
{
  @UI.facet: [{ position: 010, label: 'Flight' , type: #COLLECTION, id: 'flight'},
              { position: 020, label: 'General Information' , type: #FIELDGROUP_REFERENCE, targetElement: '_Flight', targetQualifier: 'GenInf', parentId: 'flight'},
              { position: 030, label: 'Pricing' , type: #FIELDGROUP_REFERENCE, targetElement: '_Flight', targetQualifier: 'Pricing', parentId: 'flight'},
              { position: 040, label: 'Booking' , type: #COLLECTION, id: 'booking'},
              { position: 050, label: 'Connection Information', type: #FIELDGROUP_REFERENCE, targetQualifier: 'connection', parentId: 'booking'},
              { position: 060, label: 'Flight Information', type: #FIELDGROUP_REFERENCE, targetQualifier: 'flight', parentId: 'booking'},
              { position: 070, label: 'Booking Information', type: #FIELDGROUP_REFERENCE, targetQualifier: 'booking', parentId: 'booking'},
              { position: 080, label: 'Customer Information', type: #FIELDGROUP_REFERENCE, targetQualifier: 'customer', parentId: 'booking'},
              { position: 090, label: 'Miscellaneous', type: #FIELDGROUP_REFERENCE, targetQualifier: 'miscs', parentId: 'booking'},
              { position: 100, label: 'Customer' , type: #COLLECTION, id: 'customer' },
              { position: 110, label: 'General Information', type: #FIELDGROUP_REFERENCE, targetElement: '_Customer', targetQualifier: 'General', parentId: 'customer'},
              { position: 120, label: 'Address Information', type: #FIELDGROUP_REFERENCE, targetElement: '_Customer', targetQualifier: 'Address', parentId: 'customer'},
              { position: 130, label: 'Contact Information', type: #FIELDGROUP_REFERENCE, targetElement: '_Customer', targetQualifier: 'Contact', parentId: 'customer'},
              { position: 140, label: 'Miscellaneous', type: #FIELDGROUP_REFERENCE, targetElement: '_Customer', targetQualifier: 'miscs', parentId: 'customer'}]

  @Search: {
      defaultSearchElement: true,
      fuzzinessThreshold: 0.7
  }
  @UI: {
      lineItem: [{position: 10, importance: #MEDIUM }],
      fieldGroup: [{position: 10, qualifier: 'flight' }]
  }
  CarrierID;

  @UI: {
      lineItem: [{position: 20, importance: #MEDIUM }],
      fieldGroup: [{position: 20, qualifier: 'flight' }]
  }
  ConnectionID;

  @UI: {
      lineItem: [{position: 30, importance: #MEDIUM }],
      fieldGroup: [{position: 30, qualifier: 'flight' }]
  }
  FlightDate;

  @UI: {
      lineItem: [{position: 40, importance: #HIGH }],
      fieldGroup: [{position: 40, qualifier: 'booking' }]
  }
  @EndUserText.label: 'Booking Number'
  BookingID;

  @UI: {
      lineItem: [{position: 50, importance: #MEDIUM }],
      fieldGroup: [{position: 50, qualifier: 'customer' }]
  }
  CustomerID;

  @UI: {
      fieldGroup: [{position: 60, qualifier: 'customer' }]
  }
  @EndUserText.label: 'Customer Type'
  CustomerType;

  @UI: {
      fieldGroup: [{position: 70, qualifier: 'customer' }]
  }
  IsSmoker;

  @UI: {
      lineItem: [{position: 80, importance: #MEDIUM }],
      fieldGroup: [{position: 80, qualifier: 'booking' }]
  }
  @EndUserText.label: 'Luggage Weight'
  LuggageWeight;

  @UI: {
      fieldGroup: [{position: 81, qualifier: 'booking' }]
  }
  WeightUnit;

  @UI: {
      fieldGroup: [{position: 90, qualifier: 'miscs' }]
  }
  Invoice;

  @UI: {
      fieldGroup: [{position: 100, qualifier: 'booking' }]
  }
  @EndUserText.label: 'Flight Class'
  FlightClass;

  @UI: {
      lineItem: [{position: 110, importance: #MEDIUM }],
      fieldGroup: [{position: 110, qualifier: 'booking' }]
  }
  @EndUserText.label: 'Booking Price'
  Amount;

  @UI: {
      fieldGroup: [{position: 111, qualifier: 'booking' }]
  }
  @EndUserText.label: 'Currency'
  Currency;

  @UI: {
      lineItem: [{position: 120, importance: #MEDIUM }],
      fieldGroup: [{position: 120, qualifier: 'booking' }]
  }
  @EndUserText.label: 'Booking Date'
  OrderDate;

  @UI: {
      fieldGroup: [{position: 130, qualifier: 'miscs' }]
  }
  Counter;

  @UI: {
      fieldGroup: [{position: 140, qualifier: 'miscs' }]
  }
  Agencynum;

  @UI: {
      fieldGroup: [{position: 150, qualifier: 'miscs' }]
  }
  IsCancelled;

  @UI: {
      fieldGroup: [{position: 160, qualifier: 'miscs' }]
  }
  Reserved;

  @UI: {
      fieldGroup: [{position: 52, qualifier: 'customer' }]
  }
  Passname;

  @UI: {
      fieldGroup: [{position: 51, qualifier: 'customer' }]
  }
  Passform;

  @UI: {
      fieldGroup: [{position: 190, qualifier: 'customer' }]
  }
  @EndUserText.label: 'Date of Birth'
  Passbirth;

  @UI: {
    lineItem: [{position: 52, importance: #HIGH }]
  }
  @EndUserText.label: 'Passenger Name'
  FormName;

}